#----------------------------------------------------------
# Lattice, Brillouin
#----------------------------------------------------------

"""
    SquareLattice{T}

Square lattice described by x and y.
"""
struct SquareLattice{T} 
    x::T
    y::T
end

"""
    IrreducibleBrillouin

Irreducible Brillouin zone of square lattices. It is a triangle with three 
vertices: Γ, X, and M.

# Fields

- `Γ`: Center of the Brillouin zone (0, 0)
- `X`: (π/x, 0)
- `M`: (π/x, π/y)

Here {x, y} is the basis of the square lattice.
"""
struct IrreducibleBrillouin{T}
    Γ::Vector{T}
    X::Vector{T}
    M::Vector{T}
end

function IrreducibleBrillouin(sl::SquareLattice{T}) where{T}
    Γ = zeros(T, 2)
    X = [T(π)/sl.x, zero(T)]
    M = [T(π)/sl.x, T(π)/sl.y]

    return IrreducibleBrillouin(Γ, X, M)
end

"""
    get_discrete_IrreducibleBrillouin(ibz::IrreducibleBrillouin{T}, n::Int64) where{T}

Get discrete points on the boundary of the irreducible Brillouin zone (a 
triangle for square lattices): Γ -> X -> M -> Γ.

# Arguments

- `ibz`: the irreducible Brillouin zone
- `n`: the number of points in the interior of each edge
"""
function get_discrete_IrreducibleBrillouin(ibz::IrreducibleBrillouin{T}, n::Int64) where{T}
    # Add check for h
   
    dibz = Vector{T}[]
    para = T[]

    hx = ibz.X[1]/(n + 1)
    hy = ibz.M[2]/(n + 1)
    
    L₁ = norm(ibz.X - ibz.Γ) # the length of the boundary from Γ to X
    L₂ = norm(ibz.M - ibz.X) # the length of the boundary from X to M 
    L₃ = norm(ibz.Γ - ibz.M) # the length of the boundary from M to Γ
    
    L = L₁ + L₂ + L₃ # the total length

    # Points on Γ -> X 
    push!(dibz, ibz.Γ)
    push!(para, zero(T)) 

    for i in 1:n
        p = [i * hx, zero(T)]
        push!(dibz, p)
        push!(para, (i * hx) / L)
    end
    
    # Points on X -> M 
    push!(dibz, ibz.X)
    push!(para, ibz.X[1] / L)
    
    for i in 1:n 
        p = [ibz.X[1], i * hy]
        push!(dibz, p)
        push!(para, (i * hy + ibz.X[1]) / L)
    end

    # Points on M -> Γ
    push!(dibz, ibz.M)
    push!(para, (L₁ + L₂) / L)
    
    for i in 1:n
        p = [(n + 1 - i) * hx, (n + 1 - i) * hy]
        push!(dibz, p)
        push!(para, (L₁ + L₂ + i * L₃ / (n + 1)) / L)
    end
    
    push!(dibz, ibz.Γ)
    push!(para, one(T))
    
    return dibz, para
end

#----------------------------------------------------------
# FEM codes
#----------------------------------------------------------

"""
    setup_grid_squareLattice(;lc=0.05, period=2π)

Generate a mesh for a square lattice of dielectric columns.

# Arguments

- `lc`: the mesh size
- `period`: the period of the square
"""
function setup_grid_squareLattice(;lc=0.05, period=2π)
    # Initialize gmsh 
    gmsh.initialize()
    gmsh.option.setNumber("General.Verbosity", 2)
    
    # Add the points
    # For the square
    p1 = gmsh.model.geo.addPoint(period/2, -period/2, 0, lc)
    p2 = gmsh.model.geo.addPoint(period/2, period/2, 0, lc)
    p3 = gmsh.model.geo.addPoint(-period/2, period/2, 0, lc)
    p4 = gmsh.model.geo.addPoint(-period/2, -period/2, 0, lc)

    # Add the lines
    l1 = gmsh.model.geo.addLine(p1, p2)
    l2 = gmsh.model.geo.addLine(p2, p3)
    l3 = gmsh.model.geo.addLine(p3, p4)
    l4 = gmsh.model.geo.addLine(p4, p1)
    
    # Create the loops and the domain
    loop = gmsh.model.geo.addCurveLoop([l1, l2, l3, l4])
    surf = gmsh.model.geo.addPlaneSurface([loop])
    
    # Synchronize the model
    gmsh.model.geo.synchronize()
    
    # Create physical domains
    gmsh.model.addPhysicalGroup(1, [l1], -1, "right")
    gmsh.model.addPhysicalGroup(1, [l2], -1, "top")
    gmsh.model.addPhysicalGroup(1, [l3], -1, "left")
    gmsh.model.addPhysicalGroup(1, [l4], -1, "bottom")
    gmsh.model.addPhysicalGroup(2, [surf], -1, "Cell")
 
    # Set Periodic boundary condition
    transform1 = [1, 0, 0, period, 0, 1, 0, 0, 0, 0, 1, 0, 0, 0, 0, 1]
    transform2 = [1, 0, 0, 0, 0, 1, 0, period, 0, 0, 1, 0, 0, 0, 0, 1]
    gmsh.model.mesh.setPeriodic(1, [l1], [l3], transform1)
    gmsh.model.mesh.setPeriodic(1, [l2], [l4], transform2)

    # Generate a 2D mesh
    gmsh.model.mesh.generate(2)
    
    # Read the .msh file by FerriteGmsh
    grid = mktempdir() do dir 
        path = joinpath(dir, "mesh.msh")
        gmsh.write(path)
        togrid(path)
    end

    gmsh.finalize()
    
    return grid
end

function error_biperiodic_bdcs(dh::DofHandler; px=2π, py=2π)
    cst = ConstraintHandler(dh)
    
    # Periodic boundary condition on the left and right
    pfacets_x = collect_periodic_facets(dh.grid, "right", "left", x -> x + Ferrite.Vec{2}((px, 0.0)))
    pbc_x = PeriodicDirichlet(:u, pfacets_x)
    add!(cst, pbc_x)

    # Periodic boundary condition on the top and bottom 
    pfacets_y = collect_periodic_facets(dh.grid, "top", "bottom", x -> x + Ferrite.Vec{2}((0.0, py)))
    pbc_y = PeriodicDirichlet(:u, pfacets_y)
    add!(cst, pbc_y)
    
    close!(cst)
    return cst
end
 
function setup_biperiodic_bdcs(dh::DofHandler)
    cst = ConstraintHandler(dh)
    
    bi = PeriodicDirichlet(:u, ["left" => "right", "bottom" => "top"])
    add!(cst, bi)
    close!(cst)

    return cst
end

# This function is same as the function used in ClosedWaveguideDisperion.jl
function allocate_matries(dh::DofHandler, cst::ConstraintHandler)
    sp = init_sparsity_pattern(dh)
    add_cell_entries!(sp, dh)
    add_constraint_entries!(sp, cst)
    K = allocate_matrix(SparseMatrixCSC{ComplexF64, Int}, sp)

    return K
end

function assemble_A_TM(cv::CellValues, dh::DofHandler, A::SparseMatrixCSC, α::Vector{T}) where{T}
    # Preallocate the local matrix
    n_basefuncs = getnbasefunctions(cv)
    Ae = zeros(ComplexF64, n_basefuncs, n_basefuncs)

    # Create an assembler
    assembler = start_assemble(A)

    # Loop over all cells
    for cell in CellIterator(dh)
        # Reinitialize cellvalues for this cell
        reinit!(cv, cell)

        # Reset local matrix to 0.0 + 0.0im
        fill!(Ae, 0.0 + 0.0im)

        # Loop over quadrature points
        for qp in 1:getnquadpoints(cv)
            dx = getdetJdV(cv, qp)

            # Loop over test shape functions
            for i in 1:n_basefuncs
                v = shape_value(cv, qp, i)
                ∇v = shape_gradient(cv, qp, i)

                # Loop over trial shape functions
                for j in 1:n_basefuncs
                    u = shape_value(cv, qp, j)
                    ∇u = shape_gradient(cv, qp, j)

                    # Compute the local matrix according to the variational formulation
                    Ae[i, j] += (∇u ⋅ ∇v - 2im * (α ⋅ ∇u) * v + (α ⋅ α) * u * v) * dx
                end
            end
        end

        assemble!(assembler, celldofs(cell), Ae)
    end

    return A
end

function assemble_B_TM(cv::CellValues, dh::DofHandler, B::SparseMatrixCSC, ϵ::Function)
    # Preallocate the local matrix
    n_basefuncs = getnbasefunctions(cv)
    Be = zeros(ComplexF64, n_basefuncs, n_basefuncs)

    # Create an assembler
    assembler = start_assemble(B)

    # Loop over all cells
    for cell in CellIterator(dh)
        # Reinitialize cellvalues for this cell
        reinit!(cv, cell)

        # Reset local matrix to 0.0 + 0.0im
        fill!(Be, 0.0 + 0.0im)

        # Get the coordinates of this cell
        coords = getcoordinates(cell)

        # Loop over quadrature points
        for qp in 1:getnquadpoints(cv)
            dx = getdetJdV(cv, qp)

            # Get the coordinates of the quadrature point
            # and evaluate the refractive index at this point
            coords_qp = spatial_coordinate(cv, qp, coords)
            ri = ϵ(coords_qp)

            # Loop over test shape functions
            for i in 1:n_basefuncs
                v = shape_value(cv, qp, i)

                # Loop over trial shape functions
                for j in 1:n_basefuncs
                    u = shape_value(cv, qp, j)

                    # Compute the local matrix according to the variational formulation
                    Be[i, j] += (ri * u * v) * dx
                end
            end
        end

        assemble!(assembler, celldofs(cell), Be)
    end

    return B
end

function calc_diagram_TM(cv::CellValues, dh::DofHandler, cst::ConstraintHandler, A::SparseMatrixCSC, B::SparseMatrixCSC, ϵ::Function, dibz; nevs::Int = 4)
    m = length(dibz)
    μ = zeros(m, nevs)
    
    # Assemble the matrix B
    B = assemble_B_TM(cv, dh, B, ϵ)

    # Impose the boundary conditions
    apply!(B, cst)
    
    # Assemble the matrix A at α in dibz
    for (i, α) in enumerate(dibz)
        # Assemble A at α
        A = assemble_A_TM(cv, dh, A, α)
        
        # Impose the boundary condition
        apply!(A, cst)
        
        # Solve the generalized eigenvalue problem by Arpack.jl
        λ, _ = eigs(A, B, nev = nevs, which = :SM, maxiter=1000, check=1)
        @show λ
        λ = real(λ)
        μ[i, :] = λ
        
        # Reset A to 0.0 + 0.0im
        fill!(A, 0.0 + 0.0im)
    end
    
    return μ
end
 
function assemble_A_TE(cv::CellValues, dh::DofHandler, A::SparseMatrixCSC, ϵ::Function, α::Vector{T}) where{T}
    # Preallocate the local matrix
    n_basefuncs = getnbasefunctions(cv)
    Ae = zeros(ComplexF64, n_basefuncs, n_basefuncs)

    # Create an assembler
    assembler = start_assemble(A)

    # Loop over all cells
    for cell in CellIterator(dh)
        # Reinitialize cellvalues for this cell
        reinit!(cv, cell)

        # Reset local matrix to 0.0 + 0.0im
        fill!(Ae, 0.0 + 0.0im)
        
        # Get the coordinates of this cell
        coords = getcoordinates(cell)

        # Loop over quadrature points
        for qp in 1:getnquadpoints(cv)
            dx = getdetJdV(cv, qp)
            
            # Get the coordinates of the quadrature point
            # and evaluate the refractive index at this point
            coords_qp = spatial_coordinate(cv, qp, coords)
            ri = ϵ(coords_qp)

            # Loop over test shape functions
            for i in 1:n_basefuncs
                v = shape_value(cv, qp, i)
                ∇v = shape_gradient(cv, qp, i)

                # Loop over trial shape functions
                for j in 1:n_basefuncs
                    u = shape_value(cv, qp, j)
                    ∇u = shape_gradient(cv, qp, j)

                    # Compute the local matrix according to the variational formulation
                    Ae[i, j] += ((∇u ⋅ ∇v + im * u * (α ⋅ ∇v) - im * (α ⋅ ∇u) * v + (α ⋅ α) * u * v)/ri) * dx
                end
            end
        end

        assemble!(assembler, celldofs(cell), Ae)
    end

    return A
end

function assemble_B_TE(cv::CellValues, dh::DofHandler, B::SparseMatrixCSC)
     # Preallocate the local matrix
    n_basefuncs = getnbasefunctions(cv)
    Be = zeros(ComplexF64, n_basefuncs, n_basefuncs)

    # Create an assembler
    assembler = start_assemble(B)

    # Loop over all cells
    for cell in CellIterator(dh)
        # Reinitialize cellvalues for this cell
        reinit!(cv, cell)

        # Reset local matrix to 0.0 + 0.0im
        fill!(Be, 0.0 + 0.0im)

        # Loop over quadrature points
        for qp in 1:getnquadpoints(cv)
            dx = getdetJdV(cv, qp)

            # Loop over test shape functions
            for i in 1:n_basefuncs
                v = shape_value(cv, qp, i)

                # Loop over trial shape functions
                for j in 1:n_basefuncs
                    u = shape_value(cv, qp, j)

                    # Compute the local matrix according to the variational formulation
                    Be[i, j] += (u * v) * dx
                end
            end
        end

        assemble!(assembler, celldofs(cell), Be)
    end

    return B
end

function calc_diagram_TE(cv::CellValues, dh::DofHandler, cst::ConstraintHandler, A::SparseMatrixCSC, B::SparseMatrixCSC, ϵ::Function, dibz; nevs::Int = 4)
    m = length(dibz)
    μ = zeros(m, nevs)
    
    # Assemble the matrix B
    B = assemble_B_TE(cv, dh, B)

    # Impose the boundary conditions
    apply!(B, cst)
    
    # Assemble the matrix A at α in dibz
    for (i, α) in enumerate(dibz)
        # Assemble A at α
        A = assemble_A_TE(cv, dh, A, ϵ, α)
        
        # Impose the boundary condition
        apply!(A, cst)
        
        # Solve the generalized eigenvalue problem by Arpack.jl
        λ, _ = eigs(A, B, nev = nevs, which = :SR, maxiter=1000, check=1)
        @show λ
        λ = real(λ)
        μ[i, :] = λ
        
        # Reset A to 0.0 + 0.0im
        fill!(A, 0.0 + 0.0im)
    end
    
    return μ 
end

function plot_diagram(para, μ; title, color=:tomato)
    fig = Figure()
    axi = Axis(fig[1, 1], title = title, limits = (0, 1, 0, nothing), xticks = ([0, 0.2928932188134525, 0.585786437626905, 1], ["Γ", "X", "M", "Γ"]))

    # hidexdecorations!(axi)
    # vlines!(axi, [0, 0.2928932188134525, 0.585786437626905, 1], color = :black)

    for band in eachcol(μ)
        lines!(axi, para, band, color = color) 
    end
    
    fig
end

function dofs_on_facetset(dh::DofHandler, field::Symbol, facetset)
    dtn_ch = ConstraintHandler(dh)
    dbc = Dirichlet(field, facetset, x -> 0)
    add!(dtn_ch, dbc)
    close!(dtn_ch)
    
    return dtn_ch.prescribed_dofs
end

function is_interior_top(x)
    if x[2] == 2π/2
        return x[1] > -2π/2 && x[1] < 2π/2
    else
        return false
    end
end

function is_interior_bottom(x)
    if x[2] == -2π/2
        return x[1] > -2π/2 && x[1] < 2π/2
    else
        return false
    end
end

function is_interior_left(x)
    if x[1] == -2π/2
        return x[2] > -2π/2 && x[2] < 2π/2
    else
        return false
    end
end

function is_interior_right(x)
    if x[1] == 2π/2
        return x[2] > -2π/2 && x[2] < 2π/2
    else
        return false
    end
end